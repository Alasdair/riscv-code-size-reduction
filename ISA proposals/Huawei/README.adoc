= Code-size reduction ISA extension status

This shows the code-size reduction status of proposed ISA extensions on the four main benchmarks.
Huawei IoT (2 cores - application and protocol)
Zephyr (2 cores - central and peripheral)

[NOTE]

  We need more large, real benchmarks which we can recompile with different compiler options

[NOTE]

  The baseline code-size for these tables is to compile the benchmarks on HCC with `-msave-restore`

== code size reduction examples

=== Example 1 from Zephyr

[source,sourceCode,text]
----
from lib/os/heap.c
static void split_chunks(struct z_heap *h, chunkid_t lc, chunkid_t rc)
{
        CHECK(rc > lc);
        CHECK(rc - lc < chunk_size(h, lc));

        size_t sz0 = chunk_size(h, lc);
        size_t lsz = rc - lc;
        size_t rsz = sz0 - lsz;

        set_chunk_size(h, lc, lsz);
        set_chunk_size(h, rc, rsz);
        set_left_chunk_size(h, rc, lsz);
        set_left_chunk_size(h, right_chunk(h, rc), rsz);
}
----

compiles with GCC10 to:

[source,sourceCode,text]
----
204011fe <split_chunks>:
204011fe:	7179                	addi	sp,sp,-48 ;# PUSH(1)
20401200:	d606                	sw	ra,44(sp) ;# PUSH(2)
20401202:	d422                	sw	s0,40(sp) ;# PUSH(3)
20401204:	d226                	sw	s1,36(sp) ;# PUSH(4)
20401206:	d04a                	sw	s2,32(sp) ;# PUSH(5)
20401208:	ce4e                	sw	s3,28(sp) ;# PUSH(6)
2040120a:	8432                	mv	s0,a2 ;# PUSH(7)
2040120c:	84aa                	mv	s1,a0 ;# PUSH(8)
2040120e:	892e                	mv	s2,a1 ;# PUSH(9) - but requires 32-bit encoding
20401210:	3d21                	jal	20401028 <chunk_size>
20401212:	412406b3          	sub	a3,s0,s2
20401216:	408909b3          	sub	s3,s2,s0
2040121a:	99aa                	add	s3,s3,a0
2040121c:	8636                	mv	a2,a3
2040121e:	85ca                	mv	a1,s2 ;#MVA01S(1)
20401220:	8526                	mv	a0,s1 ;#MVA01S(2)
20401222:	c636                	sw	a3,12(sp)
20401224:	3591                	jal	20401068 <set_chunk_size>
20401226:	864e                	mv	a2,s3
20401228:	85a2                	mv	a1,s0 ;#MVA01S(1)
2040122a:	8526                	mv	a0,s1 ;#MVA01S(2)
2040122c:	3d35                	jal	20401068 <set_chunk_size>
2040122e:	46b2                	lw	a3,12(sp)
20401230:	4601                	li	a2,0
20401232:	85a2                	mv	a1,s0 ;#MVA01S(1)
20401234:	8526                	mv	a0,s1 ;#MVA01S(2)
20401236:	3bd1                	jal	2040100a <chunk_set>
20401238:	85a2                	mv	a1,s0 ;#MVA01S(1)
2040123a:	8526                	mv	a0,s1 ;#MVA01S(2)
2040123c:	33f5                	jal	20401028 <chunk_size>
2040123e:	00a405b3          	add	a1,s0,a0
20401242:	5422                	lw	s0,40(sp) ;#POP(1)
20401244:	50b2                	lw	ra,44(sp) ;#POP(2)
20401246:	5902                	lw	s2,32(sp) ;#POP(3)
20401248:	86ce                	mv	a3,s3
2040124a:	8526                	mv	a0,s1 ;#POP(4) - but requires 32-bit encoding
2040124c:	49f2                	lw	s3,28(sp) ;#POP(5)
2040124e:	5492                	lw	s1,36(sp) ;#POP(6)
20401250:	4601                	li	a2,0
20401252:	6145                	addi	sp,sp,48 ;#POP(7)
20401254:	bb5d                	j	2040100a <chunk_set>
----

reduces to:

[source,sourceCode,text]
----
204011fe <split_chunks>:
204011fe:	<32-bit>                push {ra,s0-s2},{a0-a2},-48
20401210:	3d21                	jal	20401028 <chunk_size>
20401212:	412406b3          	sub	a3,s0,s2
20401216:	408909b3          	sub	s3,s2,s0
2040121a:	99aa                	add	s3,s3,a0
2040121c:	8636                	mv	a2,a3
2040121e:	<16-bit>                mv	(a0,a1),(s1,s2)
20401222:	c636                	sw	a3,12(sp)
20401224:	3591                	jal	20401068 <set_chunk_size>
20401226:	864e                	mv	a2,s3
2040122a:	8526                	mv	(a0,a1),(s1,s0)
2040122c:	3d35                	jal	20401068 <set_chunk_size>
2040122e:	46b2                	lw	a3,12(sp)
20401230:	4601                	li	a2,0
20401234:	8526                	mv	(a0,a1), (s1, s0)
20401236:	3bd1                	jal	2040100a <chunk_set>
2040123a:	8526                	mv	(a0, a1), (s1, s0) 
2040123c:	33f5                	jal	20401028 <chunk_size>
2040123e:	00a405b3          	add	a1,s0,a0
20401248:	86ce                	mv	a3,s3
20401250:	4601                	li	a2,0
20401252:	<32-bit>                pop {ra, s0-s2}, {s1}, 48
20401254:	bb5d                	j	2040100a <chunk_set>
----

88-bytes to 56-bytes.

== Example 2 from Zephyr

[source,sourceCode,text]
----
from subsys/bluetooth/controller/crypto/crypto.c
int bt_rand(void *buf, size_t len)
{
        return lll_csrand_get(buf, len);
}
----

compiles with GCC10 to:

[source,sourceCode,text]
----
20405458 <bt_rand>:
20405458:	1141                	addi	sp,sp,-16	;#PUSH(1)
2040545a:	c04a                	sw	s2,0(sp)	;#PUSH(2)
2040545c:	70000937          	lui	s2,0x70000
20405460:	62090613          	addi	a2,s2,1568 # 70000620 <prng>
20405464:	c422                	sw	s0,8(sp)	;#PUSH(3)
20405466:	c226                	sw	s1,4(sp)	;#PUSH(4)
20405468:	c606                	sw	ra,12(sp)	;#PUSH(5)
2040546a:	842a                	mv	s0,a0		;#PUSH(6)
2040546c:	84ae                	mv	s1,a1		;#PUSH(7)
2040546e:	fdafd0ef          	jal	ra,20402c48 <tc_hmac_prng_generate>	;#TBLJAL
20405472:	577d                	li	a4,-1				;#BNEI(1)
20405474:	00e51b63          	bne	a0,a4,2040548a <bt_rand+0x32>	;#BNEI(2)
20405478:	f0fff0ef          	jal	ra,20405386 <prng_reseed.constprop.0>	;#TBLJAL
2040547c:	ed09                	bnez	a0,20405496 <bt_rand+0x3e>
2040547e:	62090613          	addi	a2,s2,1568
20405482:	85a6                	mv	a1,s1 ;#MVA01S(1)
20405484:	8522                	mv	a0,s0 ;#MVA01S(2)
20405486:	fc2fd0ef          	jal	ra,20402c48 <tc_hmac_prng_generate>	;#TBLJAL
2040548a:	87aa                	mv	a5,a0
2040548c:	4705                	li	a4,1				;#BNEI(1)
2040548e:	556d                	li	a0,-5
20405490:	00e79363          	bne	a5,a4,20405496 <bt_rand+0x3e>	;#BNEI(2)
20405494:	4501                	li	a0,0
20405496:	40b2                	lw	ra,12(sp)	;#POPRET(1)
20405498:	4422                	lw	s0,8(sp)	;#POPRET(2)
2040549a:	4492                	lw	s1,4(sp)	;#POPRET(3)
2040549c:	4902                	lw	s2,0(sp)	;#POPRET(4)
2040549e:	0141                	addi	sp,sp,16	;#POPRET(5)
204054a0:	8082                	ret			;#POPRET(6)

----

reduces to

[source,sourceCode,text]
----
20405458 <bt_rand>:
20405458:	<16-bit>                push {ra,s0-s2},{a0-a1},-16
2040545c:	70000937          	lui	s2,0x70000
20405460:	62090613          	addi	a2,s2,1568 # 70000620 <prng>
2040546e:	<16-bit>          	tbljal #x <tc_hmac_prng_generate>	
20405474:	<32-bit>          	bnei	a0,-1,2040548a <bt_rand+0x32>	
20405478:	<16-bit>          	tbljal #y <prng_reseed.constprop.0>
2040547c:	ed09                	bnez	a0,20405496 <bt_rand+0x3e>
2040547e:	62090613          	addi	a2,s2,1568
20405484:	<16-bit>                mv	(a0,a1),(s0,s1)
20405486:	fc2fd0ef          	tbljal #z <tc_hmac_prng_generate>
2040548a:	87aa                	mv	a5,a0
2040548e:	556d                	li	a0,-5
20405490:	<32-bit>          	bne	a5,1,20405496 <bt_rand+0x3e>	;#BNEI(2)
20405494:	4501                	li	a0,0
20405496:	<16-bit>                popret {ra,s0-s2}, 16

----

74-bytes to 42-bytes

== Example 3 from FPMark

This function is probably from glibc and is linked to FPMark executables

[source,sourceCode,text]
----
00018e0c <_open_r>:
   18e0c:	1141                	addi	sp,sp,-16 ;#PUSH (1)
   18e0e:	c422                	sw	s0,8(sp)  ;#PUSH (2)
   18e10:	c226                	sw	s1,4(sp)  ;#PUSH (3)
   18e12:	00cf2437          	lui	s0,0xcf2
   18e16:	84aa                	mv	s1,a0     ;#PUSH (4) - will target s0 not s1
   18e18:	852e                	mv	a0,a1     ;#MVASLIDE (1) }
   18e1a:	85b2                	mv	a1,a2     ;#MVASLIDE (2) } unlikely to implement
   18e1c:	8636                	mv	a2,a3     ;#MVASLIDE (3) }
   18e1e:	c606                	sw	ra,12(sp) ;#PUSH (4)
   18e20:	e0042e23          	sw	zero,-484(s0) # cf1e1c <errno>
   18e24:	3290f0ef          	jal	ra,2894c <_open> ;#TBLJAL
   18e28:	57fd                	li	a5,-1                       ;#BNEI (1)
   18e2a:	00f51663          	bne	a0,a5,18e36 <_open_r+0x2a>  ;#BNEI (2)
   18e2e:	e1c42783          	lw	a5,-484(s0)
   18e32:	c391                	beqz	a5,18e36 <_open_r+0x2a>
   18e34:	c09c                	sw	a5,0(s1)
   18e36:	40b2                	lw	ra,12(sp)   ;#POPRET (1)
   18e38:	4422                	lw	s0,8(sp)    ;#POPRET (2)
   18e3a:	4492                	lw	s1,4(sp)    ;#POPRET (3)
   18e3c:	0141                	addi	sp,sp,16    ;#POPRET (4)
   18e3e:	8082                	ret                 ;#POPRET (5)
----

After using many of the proposed new instructions the result is (note that I have to swap `s0` and `s1` as `push` moves `a0` into `s0`):

[source,sourceCode,text]
----
00018e0c <_open_r>:
   18e0c:	<16-bit>                push	{ra,s0-s2},{a0},-16
   18e12:	00cf2437          	lui	s1,0xcf2
   18e18:	<16-bit>                mv	(a0,a1), (a1,a2), (a2,a3)
   18e20:	e0042e23          	sw	zero,-484(s1) # cf1e1c <errno>
   18e24:	<16-bit>            	tbljal	#x
   18e2a:	<32-bit>          	bnei	a0,-1,18e36 <_open_r+0x2a>
   18e2e:	e1c42783          	lw	a5,-484(s1)
   18e32:	c391                	beqz	a5,18e36 <_open_r+0x2a>
   18e34:	c09c                	sw	a5,0(s0)
   18e36:	<16-bit>                popret	{ra,s0-s2},-16
----

This function reduces from 54-bytes to 28-bytes, ~48% reduction in size. Clearly not all functions will benefit this much and the lack of `-msave-restore` means the comparison is maybe a bit optimistic.

== Proposals which save > 0.5%

=== PUSH/POP or PUSH+MV/POP

[push_pop_status_table]
.PUSH/POP status
[width=100%,options="header",]
|===================================================================================================
| Extension       |IoT application |IoT protocol |Zephyr Central |Zephyr Peripheral
5+|Measured by compiling with HCC vs baseline
| PUSH/POP           | 4.90%  | 3.31% | 5.0%   | 4.08%  
5+|Improvement of PUSH moving a registers into s registers, over the PUSH/POP result estimated by a script
| PUSH+MV/POP        | 0.94% |1.02% | 1.55%    | 1.35%  
5+|Cumulative benefit of PUSH/POP and PUSH+MV, previous two rows added
|*PUSH+MV/POP*       | *5.84%* |*4.33%*|*6.57%* |*5.43%* 
|===================================================================================================

PUSH/POP spec is https://github.com/riscv/riscv-code-size-reduction/blob/master/ISA%20proposals/Huawei/riscv_push_pop_extension_RV32_RV64_UABI.adoc[here]

=== Table jump

[table_jump_status_table]
.Table Jump status, for jump table mode
[width=100%,options="header",]
|===================================================================================================
|                 2+|IoT application 2+|IoT protocol 2+|Zephyr Central 2+|Zephyr Peripheral
9+|Estimated by a script, so elf files don't exist, relative to the baseline above, 256 table entries
| TBLJAL            2+| 9.90%  2+| 7.37% 2+| 7.23%  2+| 6.83%  
9+|relative to the HCC PUSH/POP output above, estimated by a script, 256 table entries
| TBLJAL + PUSH/POP 2+| *12.09%*  2+| *9.22%* 2+| *9.84%*  2+| *9.92%*
|===================================================================================================

Table jump spec is https://github.com/riscv/riscv-code-size-reduction/blob/master/ISA%20proposals/Huawei/table%20jump.adoc[here]

=== 16-bit load/store byte/half unsigned

[ldstbh_status_table]
.`c.lbu/c.lhu/c.sb/c.sh` status
[width=100%,options="header",]
|===================================================================================================
|            |IoT application |IoT protocol |Zephyr Central |Zephyr Peripheral
5+|Compiled with HCC, 5-bit unsigned immediate, relative to baseline
| C.LBU/C.SB | 1.39%  | 1.80% | 2.56% | 1.88%  
5+|Compiled with HCC, 5-bit unsigned immediate, relative to baseline
| C.LHU/C.SH | 0.54%  | 0.79% | 0.68%  | 0.45%
5+|Cumulative benefit
|*Total*       |*14.02%*|*11.81%*|*13.08%*|*12.25%*
|===================================================================================================

We may decide that a shorter immediate value is acceptable for `c.lbu/c.lhu/c.sb/c.sh` or to only implement `c.lbu/c.sb` as the benefit is highest.
The proposal is https://github.com/riscv/riscv-code-size-reduction/blob/master/existing_extensions/Huawei%20Custom%20Extension/riscv_ldst_bh_extension.rst[here]

Note that the debian distro has minimal use for `c.lb/c.lh`.

=== 32-bit compare-immediate-branch

[cmpimmbr_status_table]
.compare-imediate-branch status
[width=100%,options="header",]
|===================================================================================================
|            |IoT application |IoT protocol |Zephyr Central |Zephyr Peripheral
5+|Compiled with HCC, 5-bit unsigned immediate, relative to baseline
| all cmp-imm-br | 0.79%  | 0.80% | 0.81% | 0.68%  
5+|Only BEQI/BNEI, the most commonly used two (69% of the benefit)
| BEQI/BNEI | 0.55%  | 0.55% | 0.56% | 0.47%  
5+|Cumulative benefit
|*Total*        |*14.57%*|*12.36%*|*13.64%*|*12.71%*
|===================================================================================================

The proposal is https://github.com/riscv/riscv-code-size-reduction/blob/master/existing_extensions/Huawei%20Custom%20Extension/riscv_condbr_imm_extension.rst[here]
We need to analyse the encoding space used - which includes two immediates - branch offset and compare immediate.

== Proposals which save < 0.5%

=== Multi-move

[multimove_status_table]
.multi-move status
[width=100%,options="header",]
|===================================================================================================
|            |IoT application |IoT protocol |Zephyr Central |Zephyr Peripheral
5+|Estimated with a script
| C.MVA01S | 0.32% | 0.24% | 0.56% | 0.47%
| C.MVA23S | 0.11% | 0.08% | 0.02% | 0.27%
5+|Cumulative benefit
|*Total*        |*15.00%*|*12.68%*|*14.22%*|*13.21%*
|===================================================================================================

The multi-move proposal is https://github.com/riscv/riscv-code-size-reduction/blob/master/ISA%20proposals/Huawei/multi_move.adoc[here]. `C.MVA01S` is clearly beneficial, I'll keep an open mind about `C.MVA23S` until we have more results.

=== 32-bit MULIADD

[muliadd_status_table]
.compare-imediate-branch status
[width=100%,options="header",]
|===================================================================================================
|            |IoT application |IoT protocol |Zephyr Central |Zephyr Peripheral
5+|Compiled with HCC, relative to baseline
| MULIADD    | 0.20%  | 0.32% | 0.15% | 0.10%  
5+|Cumulative benefit
|*Total*        |*15.20%*|*13.00%*|*14.36%*|*13.31%*
|===================================================================================================

Proposal is https://github.com/riscv/riscv-code-size-reduction/blob/master/existing_extensions/Huawei%20Custom%20Extension/riscv_muladd_extension.rst[here].
It takes too much encoding space, Clare Wolf has a proposal for a cheaper version:

See the https://lists.riscv.org/g/tech-bitmanip/topic/multiply_immediate_add/78289291?p=,,,20,0,0,0::recentpostdate%2Fsticky,,,20,2,0,78289291[email thread] and the 
https://docs.google.com/spreadsheets/d/1rZnfWd4_K50rtPeg-yQD4h5mKcjOoghwzNg4u30Hyx0/edit#gid=0[analysis results]

Clare also proposed `MULI` and `ADDIADD`.

Maybe these should go into the next version of bit manip?

=== `C.ZEXT[BH]`

[czext_status_table]
.`c.zext[bh]` status
[width=100%,options="header",]
|===================================================================================================
|            |IoT application |IoT protocol |Zephyr Central |Zephyr Peripheral
5+|Compiled with HCC, relative to baseline
| c.zext[bh] | 0.29%  | 0.32% | 0.17% | 0.12%  
5+|Cumulative benefit
| Total | *15.49%* | *13.32%* | *14.53%* | *13.43%*
|===================================================================================================

The benchmarks are for two 3-bit operand specifiers: `c.zext[bh] rd', rs1'`
It would be more sensible to limit the encoding space an use a single 3-bit src/dst operand: `c.zext[bh] rd'`
The instructions zero extend either a byte or half-word up to the full register width, and expand to `zext.[bh]` in the B-extension.

=== A-reg slide

Proposal is https://github.com/riscv/riscv-code-size-reduction/blob/master/ISA%20proposals/Huawei/a_reg_slide.adoc[here].

Very limited benefit 0.0% to 0.1% on benchmarks, so unlikely to implement.

=== A-S moves

[as_move_status_table]
.`c.mva01s/c.mva23s` status
[width=100%,options="header",]
|===================================================================================================
|            |IoT application |IoT protocol |Zephyr Central |Zephyr Peripheral
5+|Estimated with a script
| c.mva01s | 0.47%  | 0.37% | 0.66% | 0.58%  
| c.mva23s | 0.11%  | 0.08% | 0.02% | 0.02%
5+|Cumulative benefit
| Total | *16.07%* | *13.77%* | *15.21%* | *14.03%*
|===================================================================================================

Proposal is https://github.com/riscv/riscv-code-size-reduction/blob/master/ISA%20proposals/Huawei/multi_move.adoc[here]

== To be analysed

=== `lwgp/swgp`

Proposal is listed on https://github.com/riscv/riscv-code-size-reduction/blob/master/ISA%20proposals/Huawei/32bit_encodings.adoc[this page].
It could benefit from a separate proposal page.
I expect the benefit to be high (> 2%),

=== others

These will probably offer small improvements, I estimate around 0.1% for each. They could be considered more as speed improvements for specific algorithms which need them.

- `C.NOT/C.LSBNOT`
- `C.SEXT.*`
- `C.MUL`
- https://github.com/riscv/riscv-code-size-reduction/blob/master/existing_extensions/Huawei%20Custom%20Extension/riscv_preshifted_arithmetic.rst[preshifted arithmetic - delay to next time?]
- ...and look at the B-extension


